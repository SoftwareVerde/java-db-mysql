group 'com.softwareverde'
version '4.0.0'

apply plugin: 'java'

sourceCompatibility = 1.7

task makeJar(type: Jar) {
    manifest {
        attributes  'Implementation-Title': 'Java Database MySQL Wrapper',
                    'Implementation-Version': version
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile     group: 'com.github.softwareverde',  name: 'java-constable',         version: 'v0.4.3'
    compile     group: 'com.github.softwareverde',  name: 'java-db',                version: 'v2.3.2'
    compile     group: 'com.github.softwareverde',  name: 'java-util',              version: 'v1.3.2'
    compile     group: 'mysql',                     name: 'mysql-connector-java',   version: '5.1.47'   // v6+ requires Java 8

    testCompile group: 'junit',                     name: 'junit',                  version: '4.11'
    testCompile group: 'com.github.softwareverde',  name: 'mariaDB4j',              version: 'v1.0.4'
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (! desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

